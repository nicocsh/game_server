defmodule GameServerWeb.Api.V1.HookController do
  use GameServerWeb, :controller
  use OpenApiSpex.ControllerSpecs

  alias GameServer.Accounts.Scope
  alias GameServer.Hooks.DynamicRpcs
  alias GameServer.Hooks.HookSchemas
  alias GameServer.Hooks.PluginManager
  require Logger

  operation(:index,
    operation_id: "list_hooks",
    summary: "List available hook functions",
    tags: ["Hooks"],
    security: [%{"authorization" => []}],
    responses: [
      ok: {"OK", "application/json", %OpenApiSpex.Schema{type: :object}}
    ]
  )

  def index(conn, _params) do
    static_functions =
      PluginManager.hook_modules()
      |> Enum.flat_map(fn {plugin_name, mod} ->
        GameServer.Hooks.exported_functions(mod)
        |> Enum.map(&Map.merge(&1, %{plugin: plugin_name, dynamic: false}))
      end)

    static_keys =
      static_functions
      |> Enum.map(&{Map.get(&1, :plugin), Map.get(&1, :name)})
      |> MapSet.new()

    dynamic_functions =
      DynamicRpcs.list_all()
      |> Enum.flat_map(fn {plugin_name, exports} ->
        Enum.map(exports, fn export ->
          args = Map.get(export.meta || %{}, :args) || Map.get(export.meta || %{}, "args")
          args_list = List.wrap(args)

          arg_names =
            Enum.map(args_list, fn a ->
              Map.get(a, :name) || Map.get(a, "name") || "arg"
            end)

          arity = length(arg_names)

          doc =
            Map.get(export.meta || %{}, :description) ||
              Map.get(export.meta || %{}, "description")

          signature = to_string(export.hook) <> "(" <> Enum.join(arg_names, ", ") <> ")"

          %{
            plugin: plugin_name,
            name: export.hook,
            dynamic: true,
            meta: export.meta,
            arities: [arity],
            signatures: [
              %{
                arity: arity,
                signature: signature,
                doc: doc,
                example_args: Jason.encode!(arg_names)
              }
            ]
          }
        end)
      end)
      |> Enum.reject(fn f -> MapSet.member?(static_keys, {f.plugin, f.name}) end)

    functions =
      (static_functions ++ dynamic_functions)
      |> Enum.sort_by(&{&1.plugin, &1.name})

    json(conn, %{data: functions})
  end

  @json_schema %OpenApiSpex.Schema{
    description: "JSON object with arbitrary properties",
    type: :object,
    additionalProperties: true
  }

  @call_ok_schema %OpenApiSpex.Schema{
    type: :object,
    properties: %{
      data: @json_schema
    }
  }

  operation(:invoke,
    operation_id: "call_hook",
    summary: "Invoke a hook function",
    tags: ["Hooks"],
    security: [%{"authorization" => []}],
    request_body:
      {"Call hook", "application/json",
       %OpenApiSpex.Schema{
         type: :object,
         properties: %{
           plugin: %OpenApiSpex.Schema{type: :string},
           fn: %OpenApiSpex.Schema{type: :string},
           args: %OpenApiSpex.Schema{type: :array, items: @json_schema}
         },
         required: [:plugin, :fn]
       }},
    responses: [
      ok: {"OK", "application/json", @call_ok_schema},
      bad_request:
        {"Bad Request", "application/json",
         %OpenApiSpex.Schema{
           type: :object,
           properties: %{error: %OpenApiSpex.Schema{type: :string}}
         }},
      unauthorized:
        {"Unauthorized", "application/json",
         %OpenApiSpex.Schema{
           type: :object,
           properties: %{error: %OpenApiSpex.Schema{type: :string}}
         }}
    ]
  )

  def invoke(conn, %{"plugin" => plugin, "fn" => fn_name} = params)
      when is_binary(plugin) and is_binary(fn_name) do
    user = Scope.user(conn.assigns.current_scope)
    args = Map.get(params, "args", [])

    args = if is_list(args), do: args, else: [args]

    max_count = GameServer.Limits.get(:max_hook_args_count)
    max_size = GameServer.Limits.get(:max_hook_args_size)

    args_too_many = length(args) > max_count

    args_too_large =
      case Jason.encode(args) do
        {:ok, encoded} -> byte_size(encoded) > max_size
        _ -> true
      end

    cond do
      args_too_many ->
        conn |> put_status(:bad_request) |> json(%{error: :too_many_args, max: max_count})

      args_too_large ->
        conn
        |> put_status(:request_entity_too_large)
        |> json(%{error: :args_too_large, max_bytes: max_size})

      reserved_hook_name?(fn_name) ->
        conn
        |> put_status(:bad_request)
        |> json(%{error: :reserved_hook_name})

      true ->
        # Typed hooks (registered <FnName>Request/<FnName>Reply schemas) accept
        # a single JSON object argument and reply with a JSON map; untyped
        # hooks pass through unchanged.
        case HookSchemas.call(plugin, fn_name, {:list, args}, :map, caller: user) do
          {:ok, res} ->
            json(conn, %{data: res})

          {:error, :not_implemented} ->
            conn |> put_status(:bad_request) |> json(%{error: :not_implemented})

          {:error, :not_found} ->
            conn |> put_status(:bad_request) |> json(%{error: :plugin_not_found})

          {:error, :missing_hooks_module} ->
            conn |> put_status(:bad_request) |> json(%{error: :missing_hooks_module})

          {:error, :timeout} ->
            conn |> put_status(:bad_request) |> json(%{error: :timeout})

          {:error, reason} ->
            Logger.warning(
              "hooks/call failed plugin=#{plugin} fn=#{fn_name} reason=#{inspect(reason)}"
            )

            conn
            |> put_status(:bad_request)
            |> json(normalize_hook_error(reason))
        end
    end
  end

  def invoke(conn, _params) do
    conn |> put_status(:bad_request) |> json(%{error: :invalid_request})
  end

  defp reserved_hook_name?(fn_name) when is_binary(fn_name) do
    GameServer.Hooks.internal_hooks()
    |> Enum.any?(fn atom -> to_string(atom) == fn_name end)
  end

  defp normalize_hook_error({:function_clause, message}) when is_binary(message) do
    %{error: "function_clause", details: message}
  end

  defp normalize_hook_error({:exception, message}) when is_binary(message) do
    %{error: "exception", details: message}
  end

  defp normalize_hook_error({kind, reason}) when is_atom(kind) do
    %{error: Atom.to_string(kind), details: inspect(reason)}
  end

  defp normalize_hook_error(reason) when is_atom(reason) do
    %{error: Atom.to_string(reason)}
  end

  defp normalize_hook_error(reason) do
    %{error: "unexpected_error", details: inspect(reason)}
  end
end
